import os

libs = ['pthread',
        'boost_thread',
        'boost_system',
        'boost_filesystem',
        'boost_program_options',
        'httpserver']

cxx_flags = ['--std=c++14',
             '-g',
             '-Wall',
             '-Werror',
             '-Wpedantic',
             '-DRAPIDJSON_HAS_STDSTRING=1']

base_env = Environment(#CXX='clang++',
                       CXXFLAGS=cxx_flags,
                       LIBS=libs,
                       LIBPATH='.',
                       CPPPATH='#include')

base_env['ENV']['CPATH'] = os.environ['CPATH']
base_env['ENV']['LIBRARY_PATH'] = os.environ['LIBRARY_PATH']

src_files = Glob('src/resources/*.cpp')
src_files += Glob('src/utils/*.cpp')
src_files += Glob('src/services/*.cpp')

base_env.StaticLibrary('myroomies-server', src_files)
base_env.AppendUnique(LIBS=['myroomies-server'])
base_env.Program('myroomies-server', 'src/ServerMain.cpp')

test_env = base_env.Clone()
test_env.AppendUnique(LIBS=['gtest'])

test_files = Glob('test/unit/*.cpp')
test_files += Glob('test/unit/resources/*.cpp')
test_env.Program('myroomies-unittest', test_files)


